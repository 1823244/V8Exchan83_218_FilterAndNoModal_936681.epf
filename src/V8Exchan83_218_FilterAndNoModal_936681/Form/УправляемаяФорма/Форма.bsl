
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Проверка прав доступа должна располагаться самой первой
	Если Не ПравоДоступа("Администрирование", Метаданные) Тогда
		ВызватьИсключение НСтр("ru = 'Использование обработки в интерактивном режиме доступно только администратору.'");
	КонецЕсли;
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Объект.ЭтоИнтерактивныйРежим = Истина;
	
	ЗаголовокФормы = НСтр("ru = 'Универсальный обмен данными в формате XML (%ВерсияОбработки%) для 8.3 без модальности'");
	ЗаголовокФормы = СтрЗаменить(ЗаголовокФормы, "%ВерсияОбработки%", ВерсияОбъектаСтрокойНаСервере());
	
	Заголовок = ЗаголовокФормы;
	
	ЗаполнитьСписокТиповДоступныхДляУдаления();
	
	СистИнформ = Новый СистемнаяИнформация;
	Если Лев(СистИнформ.ВерсияПриложения,3)="8.2" Тогда
	
		Объект.МодальностьРазрешена = Истина;
		
	Иначе
		
		Выполнить("Объект.МодальностьРазрешена = Метаданные.РежимИспользованияМодальности = Метаданные.СвойстваОбъектов.РежимИспользованияМодальности.Использовать;");
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.ИмяФайлаПравил.СписокВыбора.ЗагрузитьЗначения(ПравилаОбмена.ВыгрузитьЗначения());
	Элементы.ИмяФайлаОбмена.СписокВыбора.ЗагрузитьЗначения(ЗагрузкаДанныхИхФайла.ВыгрузитьЗначения());
	Элементы.ИмяФайлаДанных.СписокВыбора.ЗагрузитьЗначения(ВыгрузкаДанныхВФайл.ВыгрузитьЗначения());
	
	ПриИзмененииПериода();
	
	ОчиститьДанныеОФайлеДляЗагрузкиДанных();
	
	ПрямаяВыгрузка = ?(Объект.НепосредственноеЧтениеВИБПриемнике, 1, 0);
	
	СохраненныйРежимЗагрузки = (Объект.РежимОбмена = "Загрузка");
	
	Если СохраненныйРежимЗагрузки Тогда
		
		// нужную страницу устанавливаем
		Элементы.ГлавнаяПанельФормы.ТекущаяСтраница = Элементы.ГлавнаяПанельФормы.ПодчиненныеЭлементы.Загрузка;
		
	КонецЕсли;
	
	ОбработатьДоступностьЭлементовУправленияТранзакциями();
	
	РазвернутьСтрокиДерева(УдаляемыеДанные, Элементы.УдаляемыеДанные, "Пометка");
	
	АрхивироватьФайлПриИзмененииЗначения();
	ПрямаяВыгрузкаПриИзмененииЗначения();
	
	ИзменитьРежимОбработки(ЭтоКлиент);
	
	Если СохраненныйРежимЗагрузки
		И Объект.НастройкаАвтоматическойЗагрузкиДанных <> 0 Тогда
		
		Если Объект.НастройкаАвтоматическойЗагрузкиДанных = 1 Тогда
			
			ОтветПользователя = Вопрос(НСтр("ru = 'Выполнить загрузку данных из файла обмена?'"), РежимДиалогаВопрос.ДаНет, 15, КодВозвратаДиалога.Да);
	
			Если ОтветПользователя <> КодВозвратаДиалога.Да Тогда
					
				Возврат;
					
			КонецЕсли;
	
		КонецЕсли;
	
		ВыполнитьЗагрузкуИзФормы();
		ПредставлениеПериодаВыгрузки = ПредставлениеПериода(Объект.ДатаНачала, Объект.ДатаОкончания);
		
	КонецЕсли;
	
	УстановитьДоступностьКомандОтладки();
	
	ТипУдаленияРегистрацииИзмененийДляУзловОбменаПослеВыгрузкиПриИзмененииЗначения(
		Элементы.ТипУдаленияРегистрацииИзмененийДляУзловОбменаПослеВыгрузки);
	
	#Если ВебКлиент Тогда
		Элементы.СтраницыОтладкиВыгрузки.ТекущаяСтраница = Элементы.СтраницыОтладкиВыгрузки.ПодчиненныеЭлементы.ГруппаВыгрузкаВебКлиент;
		Элементы.СтраницыОтладкиЗагрузки.ТекущаяСтраница = Элементы.СтраницыОтладкиЗагрузки.ПодчиненныеЭлементы.ГруппаЗагрузкаВебКлиент;
		Объект.ФлагРежимОтладкиОбработчиков = Ложь;
	#КонецЕсли
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура АрхивироватьФайлПриИзменении(Элемент)
	
	АрхивироватьФайлПриИзмененииЗначения();
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаПравилОбменаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВыборФайла(Элемент, ИмяФайлаПравил, Истина, , Ложь, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаПравилОбменаОткрытие(Элемент, СтандартнаяОбработка)
	
	ОткрытьВПриложении(Элемент.ТекстРедактирования, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрямаяВыгрузкаПриИзменении(Элемент)
	
	ПрямаяВыгрузкаПриИзмененииЗначения();
	
КонецПроцедуры

&НаКлиенте
Процедура ГлавнаяПанельФормыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница.Имя = "Выгрузка" Тогда
		
		Объект.РежимОбмена = "Выгрузка";
		
	ИначеЕсли ТекущаяСтраница.Имя = "Загрузка" Тогда
		
		Объект.РежимОбмена = "Загрузка";
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФлагРежимОтладкиПриИзменении(Элемент)
	
	Если Объект.ФлагРежимОтладки Тогда
		
		Объект.ИспользоватьТранзакции = Ложь;
				
	КонецЕсли;
	
	ОбработатьДоступностьЭлементовУправленияТранзакциями();

КонецПроцедуры

&НаКлиенте
Процедура КоличествоОбработанныхОбъектовДляОбновленияСтатусаПриИзменении(Элемент)
	
	Если Объект.КоличествоОбработанныхОбъектовДляОбновленияСтатуса = 0 Тогда
		Объект.КоличествоОбработанныхОбъектовДляОбновленияСтатуса = 100;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаОбменаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВыборФайла(Элемент, ИмяФайлаОбмена, , , Объект.АрхивироватьФайл);
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаПротоколаОбменаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВыборФайла(Элемент, Объект.ИмяФайлаПротоколаОбмена, , "txt", Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаПротоколаОбменаЗагрузкаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВыборФайла(Элемент, Объект.ИмяФайлаПротоколаОбменаЗагрузка, , "txt", Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаДанныхНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВыборФайла(Элемент, ИмяФайлаДанных, , , Объект.АрхивироватьФайл);
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогИнформационнойБазыДляПодключенияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	
	ДиалогВыбораФайла.Заголовок = НСтр("ru = 'Выберите каталог информационной базы'");
	ДиалогВыбораФайла.Каталог = Объект.КаталогИнформационнойБазыДляПодключения;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Истина;
	
	Если ДиалогВыбораФайла.Выбрать() Тогда
		
		Объект.КаталогИнформационнойБазыДляПодключения = ДиалогВыбораФайла.Каталог;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаПротоколаОбменаОткрытие(Элемент, СтандартнаяОбработка)
	
	ОткрытьВПриложении(Элемент.ТекстРедактирования, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаПротоколаОбменаЗагрузкаОткрытие(Элемент, СтандартнаяОбработка)
	
	ОткрытьВПриложении(Элемент.ТекстРедактирования, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогИнформационнойБазыДляПодключенияОткрытие(Элемент, СтандартнаяОбработка)
	
	ОткрытьВПриложении(Элемент.ТекстРедактирования, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура АутентификацияWindowsИнформационнойБазыДляПодключенияПриИзменении(Элемент)
	
	Элементы.ПользовательИнформационнойБазыДляПодключения.Доступность = НЕ Объект.АутентификацияWindowsИнформационнойБазыДляПодключения;
	Элементы.ПарольИнформационнойБазыДляПодключения.Доступность = НЕ Объект.АутентификацияWindowsИнформационнойБазыДляПодключения;
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаПравилПриИзменении(Элемент)
	
	Файл = Новый Файл(ИмяФайлаПравил);
	Если ПустаяСтрока(ИмяФайлаПравил) Или Не Файл.Существует() Тогда
		СообщитьПользователю(НСтр("ru = 'Не найден файл правил обмена'"), "ИмяФайлаПравил");
		Возврат;
	КонецЕсли;
	
	Если ИменаФайловПравилИОбменаСовпадают() Тогда
		Возврат;
	КонецЕсли;
	
	ОтветПользователя = Вопрос(НСтр("ru = 'Загрузить правила обмена данными?'"), РежимДиалогаВопрос.ДаНет, 15, КодВозвратаДиалога.Да);
	
	Если ОтветПользователя = КодВозвратаДиалога.Да
		ИЛИ ОтветПользователя = КодВозвратаДиалога.Таймаут Тогда
		
		ВыполнитьЗагрузкуПравилОбмена();
		
	Иначе
		
		УстановитьПризнакЗагрузкиПравил(Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаОбменаОткрытие(Элемент, СтандартнаяОбработка)
	
	ОткрытьВПриложении(Элемент.ТекстРедактирования, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаОбменаПриИзменении(Элемент)
	
	ОчиститьДанныеОФайлеДляЗагрузкиДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьТранзакцииПриИзменении(Элемент)
	
	ОбработатьДоступностьЭлементовУправленияТранзакциями();
	
КонецПроцедуры

&НаКлиенте
Процедура ФлагРежимОтладкиОбработчиковЗагрузкиПриИзменении(Элемент)
	
	УстановитьДоступностьКомандОтладки();
	
КонецПроцедуры

&НаКлиенте
Процедура ФлагРежимОтладкиОбработчиковВыгрузкиПриИзменении(Элемент)
	
	УстановитьДоступностьКомандОтладки();
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаДанныхОткрытие(Элемент, СтандартнаяОбработка)
	
	ОткрытьВПриложении(Элемент.ТекстРедактирования, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаДанныхПриИзменении(Элемент)
	
	Если ПустоеЗначениеРеквизита(ИмяФайлаДанных, "ИмяФайлаДанных", Элементы.ИмяФайлаДанных.Заголовок)
		Или ИменаФайловПравилИОбменаСовпадают() Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ИмяФайлаОбмена = ИмяФайлаДанных;
	
	Файл = Новый Файл(Объект.ИмяФайлаОбмена);
	АрхивироватьФайл = (ВРЕГ(Файл.Расширение) = ВРЕГ(".zip"));
	
КонецПроцедуры

&НаКлиенте
Процедура ТипИнформационнойБазыДляПодключенияПриИзменении(Элемент)
	
	ТипИнформационнойБазыДляПодключенияПриИзмененииЗначения();
	
КонецПроцедуры

&НаКлиенте
Процедура ВерсияПлатформыИнформационнойБазыДляПодключенияПриИзменении(Элемент)
	
	Если ПустаяСтрока(Объект.ВерсияПлатформыИнформационнойБазыДляПодключения) Тогда
		
		Объект.ВерсияПлатформыИнформационнойБазыДляПодключения = "V8";
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипУдаленияРегистрацииИзмененийДляУзловОбменаПослеВыгрузкиПриИзменении(Элемент)
	
	ТипУдаленияРегистрацииИзмененийДляУзловОбменаПослеВыгрузкиПриИзмененииЗначения(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодВыгрузкиПриИзменении(Элемент)
	
	ПриИзмененииПериода();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодУдаленияПриИзменении(Элемент)
	
	ПриИзмененииПериода();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ ТаблицаПравилВыгрузки

&НаКлиенте
Процедура ТаблицаПравилВыгрузкиПередНачаломИзменения(Элемент, Отказ)
	
	Если Элемент.ТекущийЭлемент.Имя = "СсылкаНаУзелОбмена" Тогда
		
		Если Элемент.ТекущиеДанные.ЭтоГруппа Тогда
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПравилВыгрузкиПриИзменении(Элемент)
	
	Если Элемент.ТекущийЭлемент.Имя = "ПВД" Тогда
		
		ТекСтрока = Элемент.ТекущиеДанные;
		
		Если ТекСтрока.Включить = 2 Тогда
			ТекСтрока.Включить = 0;
		КонецЕсли;
		
		УстановитьПометкиПодчиненных(ТекСтрока, "Включить");
		УстановитьПометкиРодителей(ТекСтрока, "Включить");
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ УдаляемыеДанные

&НаКлиенте
Процедура УдаляемыеДанныеПриИзменении(Элемент)
	
	ТекСтрока = Элемент.ТекущиеДанные;
	
	УстановитьПометкиПодчиненных(ТекСтрока, "Пометка");
	УстановитьПометкиРодителей(ТекСтрока, "Пометка");

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ТестПодключения(Команда)
	
	ВыполнитьПодключениеКИБПриемникуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИнформациюОФайлеОбмена(Команда)
	
	АдресФайла = "";
	
	Если ЭтоКлиент Тогда
		Если Объект.МодальностьРазрешена Тогда
			
			Если Не ПоместитьФайл(АдресФайла,"Файл обмена",,,УникальныйИдентификатор) Тогда
				Возврат;
			КонецЕсли;
			
		Иначе
			
			Выполнить("
			|Оп = Новый ОписаниеОповещения(""ПолучитьИнформациюОФайлеОбменаОповещение"", ЭтаФорма);
			|НачатьПомещениеФайла( Оп,
			|	АдресФайла,  , Истина, ЭтаФорма.УникальныйИдентификатор);
			|
			|");
			
			Возврат;
		КонецЕсли;	
		
	КонецЕсли;
	
	Попытка
		
		ОткрытьФайлЗагрузкиНаСервере(АдресФайла);
		ПредставлениеПериодаВыгрузки = ПредставлениеПериода(Объект.ДатаНачала, Объект.ДатаОкончания);
		
	Исключение
		
		СообщитьПользователю(НСтр("ru = 'Не удалось прочитать файл обмена.'"));
		ОчиститьДанныеОФайлеДляЗагрузкиДанных();
		
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИнформациюОФайлеОбменаОповещение(Результат,Адрес,ВыбранноеИмяФайла,ДополнительныеПараметры) Экспорт 
	
	Попытка
		
		ОткрытьФайлЗагрузкиНаСервере(Адрес);
		ПредставлениеПериодаВыгрузки = ПредставлениеПериода(Объект.ДатаНачала, Объект.ДатаОкончания);
		
	Исключение
		
		СообщитьПользователю(НСтр("ru = 'Не удалось прочитать файл обмена.'"));
		ОчиститьДанныеОФайлеДляЗагрузкиДанных();
		
	КонецПопытки;
	
КонецПроцедуры


&НаКлиенте
Процедура УдалениеОтметитьВсе(Команда)
	
	Для Каждого Строка из УдаляемыеДанные.ПолучитьЭлементы() Цикл
		
		Строка.Пометка = 1;
		УстановитьПометкиПодчиненных(Строка, "Пометка");
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалениеОтменитьВсе(Команда)
	
	Для Каждого Строка из УдаляемыеДанные.ПолучитьЭлементы() Цикл
		Строка.Пометка = 0;
		УстановитьПометкиПодчиненных(Строка, "Пометка");
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалениеУдалить(Команда)
	
	ОтветПользователя = Вопрос(НСтр("ru = 'Удалить выбранные данные в информационной базе?'"), РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	
	Если ОтветПользователя <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Состояние(НСтр("ru = 'Выполняется удаление данных. Пожалуйста, подождите...'"));
	УдалитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаОтметитьВсе(Команда)
	
	Для Каждого Строка из Объект.ТаблицаПравилВыгрузки.ПолучитьЭлементы() Цикл
		Строка.Включить = 1;
		УстановитьПометкиПодчиненных(Строка, "Включить");
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаОтменитьВсе(Команда)
	
	Для Каждого Строка из Объект.ТаблицаПравилВыгрузки.ПолучитьЭлементы() Цикл
		Строка.Включить = 0;
		УстановитьПометкиПодчиненных(Строка, "Включить");
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаОчиститьУзлыОбмена(Команда)
	
	УстановитьУзелОбменаУСтрокДереваНаСервере(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаУстановитьУзелОбмена(Команда)
	
	Если Элементы.ТаблицаПравилВыгрузки.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьУзелОбменаУСтрокДереваНаСервере(Элементы.ТаблицаПравилВыгрузки.ТекущиеДанные.СсылкаНаУзелОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьПараметры(Команда)
	
	СохранитьПараметрыНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьПараметры(Команда)
	
	ВосстановитьПараметрыНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаОтладкиВыгрузки(Команда)
	
	Объект.ИмяФайлаПравилОбмена = ИмяФайлаНаСервереИлиКлиенте(ИмяФайлаПравил, АдресФайлаПравилВХранилище);
	
	ОткрытьФормуНастройкиОтладкиОбработчиков(Истина);
	
	Если ЭтоКлиент Тогда
		
		УдалитьФайлы(Объект.ИмяФайлаПравилОбмена);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаКлиенте(Команда)
	
	Если Не ЭтоКлиент Тогда
		
		ЭтоКлиент = Истина;
		
		ИзменитьРежимОбработки(ЭтоКлиент);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаСервере(Команда)
	
	Если ЭтоКлиент Тогда
		
		ЭтоКлиент = Ложь;
		
		ИзменитьРежимОбработки(ЭтоКлиент);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаОтладкиЗагрузки(Команда)
	
	АдресФайлаОбменаВХранилище = "";
	ИмяФайлаДляРасширения = "";
	
	Если ЭтоКлиент Тогда
		
		Если Объект.МодальностьРазрешена Тогда
			Если Не ПоместитьФайл(АдресФайлаОбменаВХранилище, "Файл обмена",ИмяФайлаДляРасширения,,УникальныйИдентификатор) Тогда
				Возврат;
			КонецЕсли;
		Иначе
			
			Выполнить("
			|Оп = Новый ОписаниеОповещения(""НастройкаОтладкиЗагрузкиОповещение"", ЭтаФорма);
			|НачатьПомещениеФайла( Оп,
			|	АдресФайлаОбменаВХранилище,  , Истина, ЭтаФорма.УникальныйИдентификатор);
			|
			|");
			
			Возврат;
		КонецЕсли;
		
	Иначе
		
		Если ПустоеЗначениеРеквизита(ИмяФайлаОбмена, "ИмяФайлаОбмена", Элементы.ИмяФайлаОбмена.Заголовок) Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Объект.ИмяФайлаОбмена = ИмяФайлаНаСервереИлиКлиенте(ИмяФайлаОбмена ,АдресФайлаОбменаВХранилище, ИмяФайлаДляРасширения);
	
	ОткрытьФормуНастройкиОтладкиОбработчиков(Ложь);
	
	Если ЭтоКлиент Тогда
		
		УдалитьФайлы(Объект.ИмяФайлаОбмена);
		
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура НастройкаОтладкиЗагрузкиОповещение(Результат,Адрес,ВыбранноеИмяФайла,ДополнительныеПараметры) Экспорт 
	
	Объект.ИмяФайлаОбмена = ИмяФайлаНаСервереИлиКлиенте(ИмяФайлаОбмена ,Адрес, ВыбранноеИмяФайла);
	
	ОткрытьФормуНастройкиОтладкиОбработчиков(Ложь);
	
	Если ЭтоКлиент Тогда
		
		УдалитьФайлы(Объект.ИмяФайлаОбмена);
		
	КонецЕсли;
	
КонецПроцедуры
			

&НаКлиенте
Процедура ВыполнитьВыгрузку(Команда)
	
	ВыполнитьВыгрузкуИзФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗагрузку(Команда)
	
	ВыполнитьЗагрузкуИзФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьПравилаОбмена(Команда)
	
	ИмяФайлаДляРасширения = "";
	
	Если ЭтоКлиент Тогда
		
		Если Объект.МодальностьРазрешена Тогда
		
			Если Не ПоместитьФайл(АдресФайлаПравилВХранилище, "Файл правил обмена", ИмяФайлаДляРасширения,,УникальныйИдентификатор) Тогда
				Возврат;
			КонецЕсли;
			
		Иначе
			
			Выполнить("
				|Оп = Новый ОписаниеОповещения(""ПрочитатьПравилаОбменаОповещение"", ЭтаФорма);
				|НачатьПомещениеФайла( Оп,
				|	АдресФайлаПравилВХранилище,  , Истина, ЭтаФорма.УникальныйИдентификатор);
				|
				|");
		
			Возврат;
		КонецЕсли;
		
	Иначе
		
		АдресФайлаПравилВХранилище = "";
		Если ПустоеЗначениеРеквизита(Объект.ИмяФайлаПравилОбмена, "ИмяФайлаПравил", Элементы.ИмяФайлаПравил.Заголовок) Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Состояние(НСтр("ru = 'Выполняется чтение правил обмена. Пожалуйста, подождите...'"));
	ВыполнитьЗагрузкуПравилОбмена(АдресФайлаПравилВХранилище, ИмяфайлаДляРасширения);
	
	Если Объект.ФлагОшибки Тогда
		
		УстановитьПризнакЗагрузкиПравил(Ложь);
		
	Иначе
		
		УстановитьПризнакЗагрузкиПравил(Истина);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьПравилаОбменаОповещение(Результат,Адрес,ВыбранноеИмяФайла,ДополнительныеПараметры) Экспорт 
	
	Состояние(НСтр("ru = 'Выполняется чтение правил обмена. Пожалуйста, подождите...'"));
	ВыполнитьЗагрузкуПравилОбмена(Адрес, ВыбранноеИмяФайла);
	АдресФайлаПравилВХранилище = Адрес;
	Если Объект.ФлагОшибки Тогда
		
		УстановитьПризнакЗагрузкиПравил(Ложь);
		
	Иначе
		
		УстановитьПризнакЗагрузкиПравил(Истина);
		
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Открывает файл обмена во внешнем приложении
//
// Параметры:
//  
// 
&НаКлиенте
Процедура ОткрытьВПриложении(ИмяФайла, СтандартнаяОбработка = Ложь)

	Файл = Новый Файл(ИмяФайла);
	
	Если Файл.Существует() Тогда
		
		ЗапуститьПриложение(ИмяФайла);
		
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьДанныеОФайлеДляЗагрузкиДанных()
	
	Объект.ВерсияПравилОбмена = "";
	Объект.ДатаВыгрузкиДанных = "";
	ПредставлениеПериодаВыгрузки = "";
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьДоступностьЭлементовУправленияТранзакциями()
	
	Элементы.ИспользоватьТранзакции.Доступность = НЕ Объект.ФлагРежимОтладки;
	
	Элементы.КоличествоОбъектовНаТранзакцию.Доступность = Объект.ИспользоватьТранзакции;
	
КонецПроцедуры

&НаКлиенте
Процедура АрхивироватьФайлПриИзмененииЗначения()
	
	Если Объект.АрхивироватьФайл Тогда
		ИмяФайлаДанных = СтрЗаменить(ИмяФайлаДанных, ".xml", ".zip");
	Иначе
		ИмяФайлаДанных = СтрЗаменить(ИмяФайлаДанных, ".zip", ".xml");
	КонецЕсли;
	
	Элементы.ПарольДляСжатияФайлаОбмена.Доступность = Объект.АрхивироватьФайл;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУзелОбменаУСтрокДерева(Дерево, УзелОбмена)
	
	Для Каждого Строка Из Дерево Цикл
		
		Если Строка.ЭтоГруппа Тогда
			
			УстановитьУзелОбменаУСтрокДерева(Строка.ПолучитьЭлементы(), УзелОбмена);
			
		Иначе
			
			Строка.СсылкаНаУзелОбмена = УзелОбмена;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ИменаФайловПравилИОбменаСовпадают()
	
	// + Гафуров А.А. от 30.09.13
	//Было:
	//Если Врег(СокрЛП(Объект.ИмяФайлаПравилОбмена)) = Врег(СокрЛП(ИмяФайлаДанных)) Тогда
	//Стало:
	Если Врег(СокрЛП(ИмяФайлаПравил)) = Врег(СокрЛП(ИмяФайлаДанных)) Тогда
	// - Гафуров А.А.
	
		СообщитьПользователю(НСтр("ru = 'Файл правил обмена не может совпадать с файлом данных.
		|Выберите другой файл для выгрузки данных.'"));
		Возврат Истина;
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции

// Заполняет дерево метаданных, доступных для удаления.
&НаСервере
Процедура ЗаполнитьСписокТиповДоступныхДляУдаления()
	
	ДеревоДанных = РеквизитФормыВЗначение("УдаляемыеДанные");
	
	ДеревоДанных.Строки.Очистить();
	
	СтрокаДерева = ДеревоДанных.Строки.Добавить();
	СтрокаДерева.Представление = "Справочники";
	
	Для каждого ОбъектМД Из Метаданные.Справочники Цикл
		
		Если Не ПравоДоступа("Удаление", ОбъектМД) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаМД = СтрокаДерева.Строки.Добавить();
		СтрокаМД.Представление = ОбъектМД.Имя;
		СтрокаМД.Метаданные = "СправочникСсылка." + ОбъектМД.Имя;
		
	КонецЦикла;
	
	СтрокаДерева = ДеревоДанных.Строки.Добавить();
	СтрокаДерева.Представление = "Планы видов характеристик";
	
	Для каждого ОбъектМД Из Метаданные.ПланыВидовХарактеристик Цикл
		
		Если Не ПравоДоступа("Удаление", ОбъектМД) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаМД = СтрокаДерева.Строки.Добавить();
		СтрокаМД.Представление = ОбъектМД.Имя;
		СтрокаМД.Метаданные = "ПланВидовХарактеристикСсылка." + ОбъектМД.Имя;
		
	КонецЦикла;
	
	СтрокаДерева = ДеревоДанных.Строки.Добавить();
	СтрокаДерева.Представление = "Документы";
	
	Для каждого ОбъектМД Из Метаданные.Документы Цикл
		
		Если Не ПравоДоступа("Удаление", ОбъектМД) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаМД = СтрокаДерева.Строки.Добавить();
		СтрокаМД.Представление = ОбъектМД.Имя;
		СтрокаМД.Метаданные = "ДокументСсылка." + ОбъектМД.Имя;
		
	КонецЦикла;
	
	СтрокаДерева = ДеревоДанных.Строки.Добавить();
	СтрокаДерева.Представление = "РегистрыСведений";
	
	Для каждого ОбъектМД Из Метаданные.РегистрыСведений Цикл
		
		Если Не ПравоДоступа("Удаление", ОбъектМД) Тогда
			Продолжить;
		КонецЕсли;
		
		Подчинен = (ОбъектМД.РежимЗаписи = Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.ПодчинениеРегистратору);
		Если Подчинен Тогда Продолжить КонецЕсли;
		
		СтрокаМД = СтрокаДерева.Строки.Добавить();
		СтрокаМД.Представление = ОбъектМД.Имя;
		СтрокаМД.Метаданные = "РегистрСведенийЗапись." + ОбъектМД.Имя;
		
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ДеревоДанных, "УдаляемыеДанные");
	
КонецПроцедуры

// Возвращает версию обработки.
&НаСервере
Функция ВерсияОбъектаСтрокойНаСервере()
	
	Возврат РеквизитФормыВЗначение("Объект").ВерсияОбъектаСтрокой();
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьЗагрузкуПравилОбмена(АдресФайлаПравилВХранилище = "", ИмяФайлаДляРасширения = "")
	
	Объект.ФлагОшибки = Ложь;
	
	ЗагрузитьПравилаОбменаИПараметрыНаСервере(АдресФайлаПравилВХранилище, ИмяФайлаДляРасширения);
	
	Если Объект.ФлагОшибки Тогда
		
		УстановитьПризнакЗагрузкиПравил(Ложь);
		
	Иначе
		
		УстановитьПризнакЗагрузкиПравил(Истина);
		РазвернутьСтрокиДерева(Объект.ТаблицаПравилВыгрузки, Элементы.ТаблицаПравилВыгрузки, "Включить");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьСтрокиДерева(ДеревоДанных, ПредставлениеНаФорме, ИмяФлажка)
	
	СтрокиДерева = ДеревоДанных.ПолучитьЭлементы();
	
	Для Каждого Строка Из СтрокиДерева Цикл
		
		ИдентификаторСтроки=Строка.ПолучитьИдентификатор();
		ПредставлениеНаФорме.Развернуть(ИдентификаторСтроки, Ложь);
		ВключитьРодителяЕслиВключеныПодчиненные(Строка, ИмяФлажка);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьРодителяЕслиВключеныПодчиненные(СтрокаДерева, ИмяФлажка)
	
	Включить = СтрокаДерева[ИмяФлажка];
	
	Для Каждого ПодчиненнаяСтрока Из СтрокаДерева.ПолучитьЭлементы() Цикл
		
		Если ПодчиненнаяСтрока[ИмяФлажка] = 1 Тогда
			
			Включить = 1;
			
		КонецЕсли;
		
		Если ПодчиненнаяСтрока.ПолучитьЭлементы().Количество() > 0 Тогда
			
			ВключитьРодителяЕслиВключеныПодчиненные(ПодчиненнаяСтрока, ИмяФлажка);
			
		КонецЕсли;
		
	КонецЦикла;
	
	СтрокаДерева[ИмяФлажка] = Включить;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПериода()
	
	Объект.ДатаНачала = ПериодВыгрузки.ДатаНачала;
	Объект.ДатаОкончания = ПериодВыгрузки.ДатаОкончания;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьПравилаОбменаИПараметрыНаСервере(АдресФайлаПравилВХранилище, ИмяФайлаДляРасширения)
	
	ИмяФайлаПравилОбмена = ИмяФайлаНаСервереИлиКлиенте(ИмяФайлаПравил ,АдресФайлаПравилВХранилище, ИмяФайлаДляРасширения);
	
	Если ИмяФайлаПравилОбмена = Неопределено Тогда
		
		Возврат;
		
	Иначе
		
		Объект.ИмяФайлаПравилОбмена = ИмяФайлаПравилОбмена;
		
	КонецЕсли;
	
	ОбъектДляСервера = РеквизитФормыВЗначение("Объект");
	ОбъектДляСервера.ТаблицаПравилВыгрузки = РеквизитФормыВЗначение("Объект.ТаблицаПравилВыгрузки");
	ОбъектДляСервера.ТаблицаНастройкиПараметров = РеквизитФормыВЗначение("Объект.ТаблицаНастройкиПараметров");
	
	ОбъектДляСервера.ЗагрузитьПравилаОбмена();
	ОбъектДляСервера.ИнициализироватьПервоначальныеЗначенияПараметров();
	ОбъектДляСервера.Параметры.Очистить();
	Объект.ФлагОшибки = ОбъектДляСервера.ФлагОшибки;
	
	Если ЭтоКлиент Тогда
		
		УдалитьФайлы(Объект.ИмяФайлаПравилОбмена);
		
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(ОбъектДляСервера.ТаблицаПравилВыгрузки, "Объект.ТаблицаПравилВыгрузки");
	ЗначениеВРеквизитФормы(ОбъектДляСервера.ТаблицаНастройкиПараметров, "Объект.ТаблицаНастройкиПараметров");
	
КонецПроцедуры

// Открывает диалог выбора файла
//
// Параметры:
//  Элемент                - Элемент управления, для которого выбираем файл
//  ПроверятьСуществование - Если Истина, то выбор отменяется если файл не существует
// 
&НаКлиенте
Процедура ВыборФайла(Элемент, ИмяСвойства, ПроверятьСуществование=Ложь, Знач РасширениеПоУмолчанию = "xml",
	АрхивироватьФайлДанных = Истина, ВыборФайлаПравил = Ложь)
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);

	Если РасширениеПоУмолчанию = "txt" Тогда
		
		ДиалогВыбораФайла.Фильтр = "Файл протокола обмена (*.txt)|*.txt";
		ДиалогВыбораФайла.Расширение = "txt";
		
	ИначеЕсли Объект.РежимОбмена = "Выгрузка" Тогда
		
		Если АрхивироватьФайлДанных Тогда
			
			ДиалогВыбораФайла.Фильтр = "Архивный файл данных (*.zip)|*.zip";
			ДиалогВыбораФайла.Расширение = "zip";
			
		ИначеЕсли ВыборФайлаПравил Тогда
			
			ДиалогВыбораФайла.Фильтр = "Файл данных (*.xml)|*.xml|Архивный файл данных (*.zip)|*.zip";
			ДиалогВыбораФайла.Расширение = "xml";
			
		Иначе
			
			ДиалогВыбораФайла.Фильтр = "Файл данных (*.xml)|*.xml";
			ДиалогВыбораФайла.Расширение = "xml";
			
		КонецЕсли; 
		
	Иначе
		
		ДиалогВыбораФайла.Фильтр = "Файл данных (*.xml)|*.xml|Архивный файл данных (*.zip)|*.zip";
		ДиалогВыбораФайла.Расширение = "xml";
		
	КонецЕсли;
	
	ДиалогВыбораФайла.Заголовок = НСтр("ru = 'Выберите файл'");
	ДиалогВыбораФайла.ПредварительныйПросмотр = Ложь;
	ДиалогВыбораФайла.ИндексФильтра = 0;
	ДиалогВыбораФайла.ПолноеИмяФайла = Элемент.ТекстРедактирования;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = ПроверятьСуществование;
	
	Если ДиалогВыбораФайла.Выбрать() Тогда
		
		ИмяСвойства = ДиалогВыбораФайла.ПолноеИмяФайла;
		
		Если Элемент = Элементы.ИмяФайлаПравил Тогда
			ИмяФайлаПравилПриИзменении(Элемент);
			
		ИначеЕсли Элемент = Элементы.ИмяФайлаОбмена Тогда
			ИмяФайлаОбменаПриИзменении(Элемент);
			
		ИначеЕсли Элемент = Элементы.ИмяФайлаДанных Тогда
			ИмяФайлаДанныхПриИзменении(Элемент);
	
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ВыполнитьПодключениеКИБПриемникуНаСервере()
	
	ОбъектДляСервера = РеквизитФормыВЗначение("Объект");
	ЗаполнитьЗначенияСвойств(ОбъектДляСервера, Объект);
	РезультатПодключения = ОбъектДляСервера.ВыполнитьПодключениеКИБПриемнику();
	
	Если РезультатПодключения <> Неопределено Тогда
		
		СообщитьПользователю(НСтр("ru = 'Подключение успешно установлено.'"));
		
	КонецЕсли;
	
КонецФункции

// Устанавливает состояние пометки у подчиненных строк строки дерева значений
// в зависимости от пометки текущей строки
//
// Параметры:
//  ТекСтрока      - Строка дерева значений
// 
&НаКлиенте
Процедура УстановитьПометкиПодчиненных(ТекСтрока, ИмяФлажка)
	
	Подчиненные = ТекСтрока.ПолучитьЭлементы();
	
	Если Подчиненные.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Строка из Подчиненные Цикл
		
		Строка[ИмяФлажка] = ТекСтрока[ИмяФлажка];
		
		УстановитьПометкиПодчиненных(Строка, ИмяФлажка);
		
	КонецЦикла;
		
КонецПроцедуры

// Устанавливает состояние пометки у родительских строк строки дерева значений
// в зависимости от пометки текущей строки
//
// Параметры:
//  ТекСтрока      - Строка дерева значений
// 
&НаКлиенте
Процедура УстановитьПометкиРодителей(ТекСтрока, ИмяФлажка)
	
	Родитель = ТекСтрока.ПолучитьРодителя();
	Если Родитель = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ТекСостояние = Родитель[ИмяФлажка];
	
	НайденыВключенные  = Ложь;
	НайденыВыключенные = Ложь;
	
	Для Каждого Строка из Родитель.ПолучитьЭлементы() Цикл
		Если Строка[ИмяФлажка] = 0 Тогда
			НайденыВыключенные = Истина;
		ИначеЕсли Строка[ИмяФлажка] = 1
			ИЛИ Строка[ИмяФлажка] = 2 Тогда
			НайденыВключенные  = Истина;
		КонецЕсли; 
		Если НайденыВключенные И НайденыВыключенные Тогда
			Прервать;
		КонецЕсли; 
	КонецЦикла;
	
	Если НайденыВключенные И НайденыВыключенные Тогда
		Включить = 2;
	ИначеЕсли НайденыВключенные И (Не НайденыВыключенные) Тогда
		Включить = 1;
	ИначеЕсли (Не НайденыВключенные) И НайденыВыключенные Тогда
		Включить = 0;
	ИначеЕсли (Не НайденыВключенные) И (Не НайденыВыключенные) Тогда
		Включить = 2;
	КонецЕсли;
	
	Если Включить = ТекСостояние Тогда
		Возврат;
	Иначе
		Родитель[ИмяФлажка] = Включить;
		УстановитьПометкиРодителей(Родитель, ИмяФлажка);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ОткрытьФайлЗагрузкиНаСервере(АдресФайла)
	
	Если ЭтоКлиент Тогда
		
		ДвоичныеДанные = ПолучитьИзВременногоХранилища (АдресФайла);
		АдресНаСервере = ПолучитьИмяВременногоФайла(".xml");
		ДвоичныеДанные.Записать(АдресНаСервере);
		Объект.ИмяФайлаОбмена = АдресНаСервере;
		
	Иначе
		
		ФайлНаСервере = Новый Файл(ИмяФайлаОбмена);
		
		Если Не ФайлНаСервере.Существует() Тогда
			
			СообщитьПользователю(НСтр("ru = 'Не найден файл обмена на сервере.'"), "ИмяФайлаОбмена");
			Возврат;
			
		КонецЕсли;
		
		Объект.ИмяФайлаОбмена = ИмяФайлаОбмена;
		
	КонецЕсли;
	
	ОбъектДляСервера = РеквизитФормыВЗначение("Объект");
	
	ОбъектДляСервера.ОткрытьФайлЗагрузки(Истина);
	
	Объект.ДатаНачала = ОбъектДляСервера.ДатаНачала;
	Объект.ДатаОкончания = ОбъектДляСервера.ДатаОкончания;
	Объект.ДатаВыгрузкиДанных = ОбъектДляСервера.ДатаВыгрузкиДанных;
	Объект.ВерсияПравилОбмена = ОбъектДляСервера.ВерсияПравилОбмена;
	Объект.Комментарий = ОбъектДляСервера.Комментарий;
	
КонецПроцедуры

// Удаляет помеченные строки дерева метаданных
//
&НаСервере
Процедура УдалитьНаСервере()
	
	ОбъектДляСервера = РеквизитФормыВЗначение("Объект");
	ДеревоУдаляемыхДанных = РеквизитФормыВЗначение("УдаляемыеДанные");
	
	ОбъектДляСервера.ИнициализироватьМенеджерыИСообщения();
	
	Для Каждого СтрокаДерева Из ДеревоУдаляемыхДанных.Строки Цикл
		
		Для Каждого СтрокаМД Из СтрокаДерева.Строки Цикл
			
			Если Не СтрокаМД.Пометка Тогда
				Продолжить;
			Конецесли;
			
			ТипСтрокой = СтрокаМД.Метаданные;
			ОбъектДляСервера.УдалитьОбъектыТипа(ТипСтрокой);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Устанавливает узел обмена у строк дерева
//
&НаСервере
Процедура УстановитьУзелОбменаУСтрокДереваНаСервере(УзелОбмена)
	
	УстановитьУзелОбменаУСтрокДерева(Объект.ТаблицаПравилВыгрузки.ПолучитьЭлементы(), УзелОбмена);
	
КонецПроцедуры

//Сохраняет значения параметров.
//
&НаСервере
Процедура СохранитьПараметрыНаСервере()
	
	ТаблицаПараметров = РеквизитФормыВЗначение("Объект.ТаблицаНастройкиПараметров");
	
	СохраняемыеПараметры = Новый Структура;
	
	Для Каждого СтрокаТаблицы Из ТаблицаПараметров Цикл
		СохраняемыеПараметры.Вставить(СтрокаТаблицы.Наименование, СтрокаТаблицы.Значение);
	КонецЦикла;
	
	ХранилищеСистемныхНастроек.Сохранить("УниверсальныйОбменданнымиXML", "Параметры", СохраняемыеПараметры);
	
КонецПроцедуры

//Восстанавливает значения параметров.
//
&НаСервере
Процедура ВосстановитьПараметрыНаСервере()
	
	ТаблицаПараметров = РеквизитФормыВЗначение("Объект.ТаблицаНастройкиПараметров");
	ВосстановленныеПараметры = ХранилищеСистемныхНастроек.Загрузить("УниверсальныйОбменданнымиXML", "Параметры");
	
	Если ТипЗнч(ВосстановленныеПараметры) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если ВосстановленныеПараметры.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Парам Из ВосстановленныеПараметры Цикл
		
		ИмяПараметра = Парам.Ключ;
		
		СтрокаТаблицы = ТаблицаПараметров.Найти(Парам.Ключ, "Наименование");
		
		Если СтрокаТаблицы <> Неопределено Тогда
			
			СтрокаТаблицы.Значение = Парам.Значение;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ТаблицаПараметров, "Объект.ТаблицаНастройкиПараметров");
	
КонецПроцедуры

//Интерактивная выгрузка данных.
//
&НаКлиенте
Процедура ВыполнитьЗагрузкуИзФормы()
	
	АдресФайла = "";
	ИмяФайлаДляРасширения = "";
	
	ДобавитьСтрокуКСпискуВыбора(Элементы.ИмяФайлаОбмена.СписокВыбора, ИмяФайлаОбмена, ЗагрузкаДанныхИхФайла);
	
	Если ЭтоКлиент Тогда
		
		Если Объект.МодальностьРазрешена Тогда
			Если Не ПоместитьФайл(АдресФайла, "Файл обмена", ИмяФайлаДляРасширения,,УникальныйИдентификатор) Тогда
				Возврат;
			КонецЕсли;
			
			Состояние(НСтр("ru = 'Выполняется загрузка данных. Пожалуйста, подождите...'"));
			ВыполнитьЗагрузкуНаСервере(АдресФайла, ИмяФайлаДляРасширения);
			
			ОткрытьДанныеПротоколовОбменаПриНеобходимости();
		Иначе
			 
			Выполнить("
			|Оп = Новый ОписаниеОповещения(""ОкончаниеПомещенияФайлаОповещение"", ЭтаФорма);
			|НачатьПомещениеФайла( Оп,
			|	АдресФайла,  , Истина, ЭтаФорма.УникальныйИдентификатор);
			|
			|");
			
		КонецЕсли;
		
	Иначе
		
		Если ПустоеЗначениеРеквизита(ИмяФайлаОбмена, "ИмяФайлаОбмена", Элементы.ИмяФайлаОбмена.Заголовок) Тогда
			Возврат;
		КонецЕсли;
		
		Состояние(НСтр("ru = 'Выполняется загрузка данных. Пожалуйста, подождите...'"));
		ВыполнитьЗагрузкуНаСервере(АдресФайла, ИмяФайлаДляРасширения);
		
		ОткрытьДанныеПротоколовОбменаПриНеобходимости();
	КонецЕсли;
	
	
КонецПроцедуры


&НаКлиенте
Процедура ОкончаниеПомещенияФайлаОповещение(Результат,Адрес,ВыбранноеИмяФайла,ДополнительныеПараметры) Экспорт 
	
	Если НЕ ЗначениеЗаполнено(ВыбранноеИмяФайла) Тогда
		Возврат;
	КонецЕсли;
	
	Состояние(НСтр("ru = 'Выполняется загрузка данных. Пожалуйста, подождите...'"));
	ВыполнитьЗагрузкуНаСервере(Адрес, ВыбранноеИмяФайла);
	
	ОткрытьДанныеПротоколовОбменаПриНеобходимости();
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьЗагрузкуНаСервере(АдресФайла, ИмяФайлаДляРасширения)
	
	ИмяЗагружаемогоФайла = ИмяФайлаНаСервереИлиКлиенте(ИмяФайлаОбмена ,АдресФайла, ИмяФайлаДляРасширения);
	
	Если ИмяЗагружаемогоФайла = Неопределено Тогда
		
		Возврат;
		
	Иначе
		
		Объект.ИмяФайлаОбмена = ИмяЗагружаемогоФайла;
		
	КонецЕсли;
	
	ОбъектДляСервера = РеквизитФормыВЗначение("Объект");
	ЗаполнитьЗначенияСвойств(ОбъектДляСервера, Объект);
	ОбъектДляСервера.ВыполнитьЗагрузку();
	
	Попытка
		
		Если Не ПустаяСтрока(АдресФайла) Тогда
			УдалитьФайлы(ИмяЗагружаемогоФайла);
		КонецЕсли;
		
	Исключение
		
	КонецПопытки;
	
	ОбъектДляСервера.Параметры.Очистить();
	ЗначениеВРеквизитФормы(ОбъектДляСервера, "Объект");
	
	ПравилаЗагружены = Ложь;
	Элементы.ФормаВыполнитьВыгрузку.Доступность = Ложь;
	Элементы.НадписьПояснениеВыгрузки.Видимость = Истина;
	Элементы.ГруппаВыгрузкаОтладкаДоступна.Доступность = Ложь;
	
КонецПроцедуры

&НаСервере
Функция ИмяФайлаНаСервереИлиКлиенте(ИмяРеквизита ,Знач АдресФайла = "", Знач ИмяФайлаДляРасширения = ".xml",
									СоздатьНовый = Ложь, ПроверятьСуществование = Истина)
	
	ИмяФайла = Неопределено;
	
	Если ЭтоКлиент Тогда
		
		Если СоздатьНовый Тогда
			
			Расширение = ? (Объект.АрхивироватьФайл, ".zip", ".xml");
			
			ИмяФайла = ПолучитьИмяВременногоФайла(Расширение);
			
			Файл = Новый Файл(ИмяФайла);
			
		Иначе
			
			Расширение = РасширениеФайла(ИмяФайлаДляРасширения);
			ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресФайла);
			АдресНаСервере = ПолучитьИмяВременногоФайла(Расширение);
			ДвоичныеДанные.Записать(АдресНаСервере);
			ИмяФайла = АдресНаСервере;
			
		КонецЕсли;
		
	Иначе
		
		ФайлНаСервере = Новый Файл(ИмяРеквизита);
		
		Если Не ФайлНаСервере.Существует() И ПроверятьСуществование Тогда
			
			СообщитьПользователю(НСтр("ru = 'Указанный файл не существует.'"));
			
		Иначе
			
			ИмяФайла = ИмяРеквизита;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ИмяФайла;
	
КонецФункции

&НаСервере
Функция РасширениеФайла(Знач ИмяФайла)
	
	ПозицияТочки = ПоследнийРазделитель(ИмяФайла);
	
	Расширение = Прав(ИмяФайла,СтрДлина(ИмяФайла) - ПозицияТочки + 1);
	
	Возврат Расширение;
	
КонецФункции

&НаСервере
Функция ПоследнийРазделитель(СтрокаСРазделителем, Разделитель = ".")
	
	ДлинаСтроки = СтрДлина(СтрокаСРазделителем);
	
	Пока ДлинаСтроки > 0 Цикл
		
		Если Сред(СтрокаСРазделителем, ДлинаСтроки, 1) = Разделитель Тогда
			
			Возврат ДлинаСтроки; 
			
		КонецЕсли;
		
		ДлинаСтроки = ДлинаСтроки - 1;
		
	КонецЦикла;

КонецФункции

&НаКлиенте
Процедура ВыполнитьВыгрузкуИзФормы()
	
	// запомним файл правил и файл выгрузки
	ДобавитьСтрокуКСпискуВыбора(Элементы.ИмяФайлаПравил.СписокВыбора, ИмяФайлаПравил, ПравилаОбмена);
	
	Если НЕ Объект.НепосредственноеЧтениеВИБПриемнике и Не ЭтоКлиент Тогда
		
		Если ИменаФайловПравилИОбменаСовпадают() Тогда
			Возврат;
		КонецЕсли;
		
		ДобавитьСтрокуКСпискуВыбора(Элементы.ИмяФайлаДанных.СписокВыбора, ИмяФайлаДанных, ВыгрузкаДанныхВФайл);
		
	КонецЕсли;
	
	Состояние(НСтр("ru = 'Выполняется выгрузка данных. Пожалуйста, подождите...'"));
	АдресФайлаДанныхВХранилище = ВыполнитьВыгрузкуНаСервере();
	
	Если АдресФайлаДанныхВХранилище = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РазвернутьСтрокиДерева(Объект.ТаблицаПравилВыгрузки, Элементы.ТаблицаПравилВыгрузки, "Включить");
	
	Если ЭтоКлиент И Не ПрямаяВыгрузка И Не Объект.ФлагОшибки Тогда
		
		ИмяСохраняемогоФайла = ?(Объект.АрхивироватьФайл, НСтр("ru = 'Файл выгрузки.zip'"),НСтр("ru = 'Файл выгрузки.xml'"));
		
		ПолучитьФайл(АдресФайлаДанныхВХранилище, ИмяСохраняемогоФайла)
		
	КонецЕсли;
	
	ОткрытьДанныеПротоколовОбменаПриНеобходимости();
	
КонецПроцедуры

&НаСервере
Функция ВыполнитьВыгрузкуНаСервере()
	
	Объект.ИмяФайлаПравилОбмена = ИмяФайлаНаСервереИлиКлиенте(ИмяФайлаПравил, АдресФайлаПравилВХранилище);
	
	Если Не ПрямаяВыгрузка Тогда
		
		ИмяВременногоФайлаДанных = ИмяФайлаНаСервереИлиКлиенте(ИмяФайлаДанных, ,,Истина, Ложь);
		
		Если ИмяВременногоФайлаДанных = Неопределено Тогда
			
			Возврат Неопределено;
			СообщитьПользователю(НСтр("ru = 'Не определен файл данных'"));
			
		Иначе
			
			Объект.ИмяФайлаОбмена = ИмяВременногоФайлаДанных;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ТаблицаПравилВыгрузки = РеквизитФормыВЗначение("Объект.ТаблицаПравилВыгрузки");
	ТаблицаНастройкиПараметров = РеквизитФормыВЗначение("Объект.ТаблицаНастройкиПараметров");
	
	// + Гафуров А.А. от 04.07.13
	АдресаСКД = Объект.АдресаМакетовСКД;
	// - Гафуров А.А.
		
	ОбъектДляСервера = РеквизитФормыВЗначение("Объект");
	ЗаполнитьЗначенияСвойств(ОбъектДляСервера, Объект);
	
	// + Гафуров А.А. от 04.07.13
	ОбъектДляСервера.АдресаМакетовСКД = АдресаСКД;
	// - Гафуров А.А.	
	
	Если ОбъектДляСервера.ФлагРежимОтладкиОбработчиков Тогда
		
		Отказ = Ложь;
		
		Файл = Новый Файл(ОбъектДляСервера.ИмяФайлаВнешнейОбработкиОбработчиковСобытий);
		
		Если Не Файл.Существует() Тогда
			
			СообщитьПользователю(НСтр("ru = 'Файл внешней обработки отладчиков событий не существует на сервере'"));
			Возврат Неопределено;
			
		КонецЕсли;
		
		ОбъектДляСервера.ВыгрузитьОбработчикиСобытий(Отказ);
		
		Если Отказ Тогда
			
			СообщитьПользователю(НСтр("ru = 'Не удалось выгрузить обработчики событий'"));
			Возврат "";
			
		КонецЕсли;
		
	Иначе
		
		ОбъектДляСервера.ЗагрузитьПравилаОбмена();
		ОбъектДляСервера.ИнициализироватьПервоначальныеЗначенияПараметров();
		
	КонецЕсли;
	
	ИзменитьДеревоПравилВыгрузки(ОбъектДляСервера.ТаблицаПравилВыгрузки.Строки, ТаблицаПравилВыгрузки.Строки);
	ИзменитьТаблицуПараметров(ОбъектДляСервера.ТаблицаНастройкиПараметров, ТаблицаНастройкиПараметров);
	
	ОбъектДляСервера.ВыполнитьВыгрузку();
	ОбъектДляСервера.ТаблицаПравилВыгрузки = РеквизитФормыВЗначение("Объект.ТаблицаПравилВыгрузки");
	
	Если ЭтоКлиент И Не ПрямаяВыгрузка Тогда
		
		АдресФайлаДанных = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(Объект.ИмяФайлаОбмена), УникальныйИдентификатор);
		УдалитьФайлы(Объект.ИмяФайлаОбмена);
		
	Иначе
		
		АдресФайлаДанных = "";
		
	КонецЕсли;
	
	Если ЭтоКлиент Тогда
		
		УдалитьФайлы(ОбъектДляСервера.ИмяФайлаПравилОбмена);
		
	КонецЕсли;
	
	ОбъектДляСервера.Параметры.Очистить();
	ЗначениеВРеквизитФормы(ОбъектДляСервера, "Объект");
	
	Возврат АдресФайлаДанных;
	
КонецФункции

&НаКлиенте
Процедура УстановитьДоступностьКомандОтладки();
	
	Элементы.НастройкаОтладкиЗагрузки.Доступность = Объект.ФлагРежимОтладкиОбработчиков;
	Элементы.НастройкаОтладкиВыгрузки.Доступность = Объект.ФлагРежимОтладкиОбработчиков;
	
КонецПроцедуры

// Изменяет дерево ПВД в соответствии с деревом на форме.
//
&НаСервере
Процедура ИзменитьДеревоПравилВыгрузки(СтрокиИсходногоДерева, СтрокиЗаменяемогоДерева)
	
	КолонкаВключить = СтрокиЗаменяемогоДерева.ВыгрузитьКолонку("Включить");
	СтрокиИсходногоДерева.ЗагрузитьКолонку(КолонкаВключить, "Включить");
	КолонкаУзел = СтрокиЗаменяемогоДерева.ВыгрузитьКолонку("СсылкаНаУзелОбмена");
	СтрокиИсходногоДерева.ЗагрузитьКолонку(КолонкаУзел, "СсылкаНаУзелОбмена");
	
	// + Гафуров А.А. от 04.07.13
	КолонкаНастройкиПостроителя =  СтрокиЗаменяемогоДерева.ВыгрузитьКолонку("НастройкаКомпоновки");
	СтрокиИсходногоДерева.ЗагрузитьКолонку(КолонкаНастройкиПостроителя, "НастройкиПостроителя");
	КолонкаИспользоватьОтбор =  СтрокиЗаменяемогоДерева.ВыгрузитьКолонку("ИспользоватьОтбор");
	СтрокиИсходногоДерева.ЗагрузитьКолонку(КолонкаИспользоватьОтбор, "ИспользоватьОтбор");
	// - Гафуров А.А.
	
	Для Каждого СтрокаИсходногоДерева Из СтрокиИсходногоДерева Цикл
		
		ИндексСтроки = СтрокиИсходногоДерева.Индекс(СтрокаИсходногоДерева);
		СтрокаИзменяемогоДерева = СтрокиЗаменяемогоДерева.Получить(ИндексСтроки);
		
		ИзменитьДеревоПравилВыгрузки(СтрокаИсходногоДерева.Строки, СтрокаИзменяемогоДерева.Строки);
		
	КонецЦикла;
	
КонецПроцедуры

// Изменяет таблицу параметров в соответствии с таблицей на форме
//
&НаСервере
Процедура ИзменитьТаблицуПараметров(ТаблицаБазы, ТаблицаФормы)
	
	КолонкаНаименование = ТаблицаФормы.ВыгрузитьКолонку("Наименование");
	ТаблицаБазы.ЗагрузитьКолонку(КолонкаНаименование, "Наименование");
	КолонкаЗначение = ТаблицаФормы.ВыгрузитьКолонку("Значение");
	ТаблицаБазы.ЗагрузитьКолонку(КолонкаЗначение, "Значение");
	
КонецПроцедуры

&НаКлиенте
Процедура ПрямаяВыгрузкаПриИзмененииЗначения()
	
	ПараметрыВыгрузки = Элементы.ПараметрыВыгрузки;
	
	ПараметрыВыгрузки.ТекущаяСтраница = ?(ПрямаяВыгрузка = 0,
										  ПараметрыВыгрузки.ПодчиненныеЭлементы.ВыгрузкаВФайл,
										  ПараметрыВыгрузки.ПодчиненныеЭлементы.ВыгрузкаВИБПриемник);
	
	Объект.НепосредственноеЧтениеВИБПриемнике = ?(ПрямаяВыгрузка = 1, Истина, Ложь);
	
	ТипИнформационнойБазыДляПодключенияПриИзмененииЗначения();
	
КонецПроцедуры

Процедура ТипИнформационнойБазыДляПодключенияПриИзмененииЗначения()
	
	ТипБазы = Элементы.ТипБазы;
	ТипБазы.ТекущаяСтраница = ?(Объект.ТипИнформационнойБазыДляПодключения,
										ТипБазы.ПодчиненныеЭлементы.ФайловаяБаза,
										ТипБазы.ПодчиненныеЭлементы.БазаНаСервере);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтрокуКСпискуВыбора(СписокСохраняемыхЗначений, ЗначениеСохранения, ИмяПараметраДляСохранения)
	
	Если ПустаяСтрока(ЗначениеСохранения) Тогда
		Возврат;
	КонецЕсли;
	
	НайденныйЭлемент = СписокСохраняемыхЗначений.НайтиПоЗначению(ЗначениеСохранения);
	Если НайденныйЭлемент <> Неопределено Тогда
		СписокСохраняемыхЗначений.Удалить(НайденныйЭлемент);
	КонецЕсли;
	
	СписокСохраняемыхЗначений.Вставить(0, ЗначениеСохранения);
	
	Пока СписокСохраняемыхЗначений.Количество() > 10 Цикл
		СписокСохраняемыхЗначений.Удалить(СписокСохраняемыхЗначений.Количество() - 1);
	КонецЦикла;
	
	ИмяПараметраДляСохранения = СписокСохраняемыхЗначений;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНастройкиОтладкиОбработчиков(ОбработчикиСобытийИзФайлаПравил)
	
	ИмяОбработки = Лев(ИмяФормы, ПоследнийРазделитель(ИмяФормы));
	ИмяВызываемойФормы = ИмяОбработки + "УправляемаяФормаНастройкиОтладкиОбработчиков";
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ИмяФайлаВнешнейОбработкиОбработчиковСобытий", Объект.ИмяФайлаВнешнейОбработкиОбработчиковСобытий);
	ПараметрыФормы.Вставить("РежимОтладкиАлгоритмов", Объект.РежимОтладкиАлгоритмов);
	ПараметрыФормы.Вставить("ИмяФайлаПравилОбмена", Объект.ИмяФайлаПравилОбмена);
	ПараметрыФормы.Вставить("ИмяФайлаОбмена", Объект.ИмяФайлаОбмена);
	ПараметрыФормы.Вставить("ОбработчикиСобытийЧитаемИзФайлаПравилОбмена", ОбработчикиСобытийИзФайлаПравил);
	ПараметрыФормы.Вставить("ИмяОбработки", ИмяОбработки);
	
	Если Объект.МодальностьРазрешена Тогда
		ПараметрыОтладки = ОткрытьФормуМодально(ИмяВызываемойФормы, ПараметрыФормы, ЭтаФорма);
	 
		Если ПараметрыОтладки <> Неопределено Тогда
			
			ЗаполнитьЗначенияСвойств(Объект, ПараметрыОтладки);
			
		КонецЕсли;
	
	Иначе
		
		
		Оповещение = Новый ОписаниеОповещения("ОткрытьФормуНастройкиОтладкиОбработчиковЗавершение", ЭтаФорма);
		ОткрытьФорму(ИмяВызываемойФормы, ПараметрыФормы, ЭтаФорма,,,,Оповещение, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
		
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНастройкиОтладкиОбработчиковЗавершение(РезультатЗакрытия, ДополнительныеПараметры)   Экспорт
	
	Если РезультатЗакрытия <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(Объект, РезультатЗакрытия);
		
	КонецЕсли;

КонецПроцедуры


&НаКлиенте
Процедура ИзменитьРасположениеФайла()
	
	Элементы.ИмяФайлаПравил.Видимость = Не ЭтоКлиент;
	Элементы.ИмяФайлаДанных.Видимость = Не ЭтоКлиент;
	Элементы.ИмяФайлаОбмена.Видимость = Не ЭтоКлиент;
	
	УстановитьПризнакЗагрузкиПравил(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьРежимОбработки(РежимРаботы)
	
	ГруппаРежима = КоманднаяПанель.ПодчиненныеЭлементы.РежимОбработки.ПодчиненныеЭлементы;
	
	ГруппаРежима.ФормаНаКлиенте.Пометка = РежимРаботы;
	ГруппаРежима.ФормаНаСервере.Пометка = Не РежимРаботы;
	
	КоманднаяПанель.ПодчиненныеЭлементы.РежимОбработки.Заголовок = 
	?(РежимРаботы, НСтр("ru = 'Режим работы (на клиенте)'"), НСтр("ru = 'Режим работы (на сервере)'"));
	
	Объект.ТаблицаПравилВыгрузки.ПолучитьЭлементы().Очистить();
	Объект.ТаблицаНастройкиПараметров.Очистить();
	
	ИзменитьРасположениеФайла();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДанныеПротоколовОбменаПриНеобходимости()
	
	Если НЕ Объект.ОткрыватьПротоколыОбменаПослеВыполненияОпераций Тогда
		Возврат;
	КонецЕсли;
	
	#Если Не ВебКлиент  Тогда
		
		Если Не ПустаяСтрока(Объект.ИмяФайлаПротоколаОбмена) Тогда
			ОткрытьВПриложении(Объект.ИмяФайлаПротоколаОбмена);
		КонецЕсли;
		
		Если Объект.НепосредственноеЧтениеВИБПриемнике Тогда
			
			Объект.ИмяФайлаПротоколаОбменаЗагрузка = ПолучитьИмяПротоколаДляВторойИнформационнойБазыComСоединенияНаСервере();
			
			Если Не ПустаяСтрока(Объект.ИмяФайлаПротоколаОбменаЗагрузка) Тогда
				ОткрытьВПриложении(Объект.ИмяПротоколаЗагрузки);
			КонецЕсли;
			
		КонецЕсли;
		
	#КонецЕсли
	
КонецПроцедуры

&НаСервере
Функция ПолучитьИмяПротоколаДляВторойИнформационнойБазыComСоединенияНаСервере()
	
	Возврат РеквизитФормыВЗначение("Объект").ПолучитьИмяПротоколаДляВторойИнформационнойБазыComСоединения();
	
КонецФункции

&НаКлиенте
Функция ПустоеЗначениеРеквизита(Реквизит, ПутьКДанным, Заголовок)
	
	Если ПустаяСтрока(Реквизит) Тогда
		
		ТекстСообщения = Нстр("ru = 'Поле ""%1"" не заполнено'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", Заголовок);
		
		СообщитьПользователю(ТекстСообщения, ПутьКДанным);
		
		Возврат Истина;
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ТипУдаленияРегистрацииИзмененийДляУзловОбменаПослеВыгрузкиПриИзмененииЗначения(Элемент)
	
	Если Элемент.ТекстРедактирования = "Не удалять регистрацию" Тогда
		
		Объект.ТипУдаленияРегистрацииИзмененийДляУзловОбменаПослеВыгрузки = 0;
		
	ИначеЕсли Элемент.ТекстРедактирования = "Полностью удалить регистрацию для узла обмена" Тогда
		
		Объект.ТипУдаленияРегистрацииИзмененийДляУзловОбменаПослеВыгрузки = 1;
		
	Иначе
		
		Объект.ТипУдаленияРегистрацииИзмененийДляУзловОбменаПослеВыгрузки = 2;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПризнакЗагрузкиПравил(Признак)
	
	ПравилаЗагружены = Признак;
	Элементы.ФормаВыполнитьВыгрузку.Доступность = Признак;
	Элементы.НадписьПояснениеВыгрузки.Видимость = Не Признак;
	Элементы.ГруппаВыгрузкаОтладкаДоступна.Доступность = Признак;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СообщитьПользователю(Текст, ПутьКДанным = "")
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Текст;
	Сообщение.ПутьКДанным = ПутьКДанным;
	Сообщение.Сообщить();
	
КонецПроцедуры

// + Гафуров А.А.

// + 04.07.13

/////////////////////////////////////////////////////////
//	СЕРВЕРНЫЕ ПРОЦЕДУРЫ ДЛЯ РАБОТЫ С ОТБОРОМ

//Настройка отбора компоновщика для отображения на форме
&НаСервереБезКонтекста
Функция НастроитьОтбор(СтруктураПВД, КомпоновщикНастроек, УИДФормы, АдресХранилищаМакетовСКД) 
	
	//Будем кэшировать созданные макеты СКД
	Если ПустаяСтрока(АдресХранилищаМакетовСКД) Или НЕ ЭтоАдресВременногоХранилища(АдресХранилищаМакетовСКД) Тогда
		МакетыСКД = Новый Соответствие;
		АдресХранилищаМакетовСКД = ПоместитьВоВременноеХранилище(МакетыСКД, УИДФормы);
	Иначе
		МакетыСКД = ПолучитьИзВременногоХранилища(АдресХранилищаМакетовСКД);
	КонецЕсли;
		
	//поиск макета скд, если нет, то создаем новый
	АдресВремХранилища = МакетыСКД.Получить(СтруктураПВД.ИмяОбъектаДляЗапроса);
	Если АдресВремХранилища = Неопределено Тогда
		//Сформируем запрос для получения колонок в запрос СКД
		ЗаголовокЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1 ";
		Запрос = Новый Запрос(ЗаголовокЗапроса + "* ИЗ " + СтруктураПВД.ИмяОбъектаДляЗапроса);
		РезультатЗапроса = Запрос.Выполнить();
		ТекстКолонок = "";
		Если РезультатЗапроса.Пустой() Тогда
			Возврат Ложь;
		Иначе
			КолКолонок = РезультатЗапроса.Колонки.Количество();
			Для Сч=0 По КолКолонок-1 Цикл
				ТекстКолонок = ТекстКолонок + РезультатЗапроса.Колонки[Сч].Имя;
				Если Сч<КолКолонок-1 Тогда
					ТекстКолонок = ТекстКолонок + ",";	
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		СКД = Новый СхемаКомпоновкиДанных;
		ИсточникДанных = СКД.ИсточникиДанных.Добавить();
		ИсточникДанных.Имя = "ИсточникДанных1";
		ИсточникДанных.ТипИсточникаДанных = "Local";
		
		НаборДанных = СКД.НаборыДанных.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));
		НаборДанных.ИсточникДанных = "ИсточникДанных1";
		НаборДанных.Имя = "НаборДанных1";
		НаборДанных.АвтоЗаполнениеДоступныхПолей = Истина;
		
		НаборДанных.Запрос = ЗаголовокЗапроса + ТекстКолонок + " ИЗ " + СтруктураПВД.ИмяОбъектаДляЗапроса; 
		
		АдресВремХранилища = ПоместитьВоВременноеХранилище(СКД, УИДФормы);
		МакетыСКД.Вставить(СтруктураПВД.ИмяОбъектаДляЗапроса, АдресВремХранилища);
		АдресХранилищаМакетовСКД = ПоместитьВоВременноеХранилище(МакетыСКД, УИДФормы);
	КонецЕсли;
	
	//инициализируем компоновщик для получения доступных полей отбора
	КомпоновщикНастроек.Настройки.Отбор.Элементы.Очистить();
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресВремХранилища));
	
	//Если отбор уже был установлен, то заполним его
	Если СтруктураПВД.НастройкиПостроителя.Элементы.Количество() > 0 Тогда
		Попытка
			КоллекцияОтборов = СтруктураПВД.НастройкиПостроителя;			
			СкопироватьЭлементы(КомпоновщикНастроек.Настройки.Отбор, КоллекцияОтборов);
		Исключение
			СтруктураПВД.НастройкиПостроителя = Неопределено;
		КонецПопытки;
	КонецЕсли;
		
	Возврат Истина;
	
КонецФункции

 // Компирует элементы из одной коллекции в другую
 &НаСервереБезКонтекста
Процедура СкопироватьЭлементы(ПриемникЗначения, ИсточникЗначения, ПроверятьДоступность = Ложь, ОчищатьПриемник = Истина) Экспорт
	
	Если ТипЗнч(ИсточникЗначения) = Тип("УсловноеОформлениеКомпоновкиДанных")
		ИЛИ ТипЗнч(ИсточникЗначения) = Тип("ВариантыПользовательскогоПоляВыборКомпоновкиДанных")
		ИЛИ ТипЗнч(ИсточникЗначения) = Тип("ОформляемыеПоляКомпоновкиДанных") Тогда
		СоздаватьПоТипу = Ложь;
	Иначе
		СоздаватьПоТипу = Истина;
	КонецЕсли;
	ПриемникЭлементов = ПриемникЗначения.Элементы;
	ИсточникЭлементов = ИсточникЗначения.Элементы;
	Если ОчищатьПриемник Тогда
		ПриемникЭлементов.Очистить();
	КонецЕсли;
	
	Для каждого ЭлементИсточник Из ИсточникЭлементов Цикл
		
		Если ТипЗнч(ЭлементИсточник) = Тип("ЭлементПорядкаКомпоновкиДанных") Тогда
			// Элементы порядка добавляем в начало
			Индекс = ИсточникЭлементов.Индекс(ЭлементИсточник);
			ЭлементПриемник = ПриемникЭлементов.Вставить(Индекс, ТипЗнч(ЭлементИсточник));
		Иначе
			Если СоздаватьПоТипу Тогда
				ЭлементПриемник = ПриемникЭлементов.Добавить(ТипЗнч(ЭлементИсточник));
			Иначе
				ЭлементПриемник = ПриемникЭлементов.Добавить();
			КонецЕсли;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ЭлементПриемник, ЭлементИсточник);
		// В некоторых коллекциях необходимо заполнить другие коллекции
		Если ТипЗнч(ИсточникЭлементов) = Тип("КоллекцияЭлементовУсловногоОформленияКомпоновкиДанных") Тогда
			СкопироватьЭлементы(ЭлементПриемник.Поля, ЭлементИсточник.Поля);
			СкопироватьЭлементы(ЭлементПриемник.Отбор, ЭлементИсточник.Отбор);
			ЗаполнитьЭлементы(ЭлементПриемник.Оформление, ЭлементИсточник.Оформление); 
		ИначеЕсли ТипЗнч(ИсточникЭлементов)	= Тип("КоллекцияВариантовПользовательскогоПоляВыборКомпоновкиДанных") Тогда
			СкопироватьЭлементы(ЭлементПриемник.Отбор, ЭлементИсточник.Отбор);
		КонецЕсли;
		
		// В некоторых элементах коллекции необходимо заполнить другие коллекции
		Если ТипЗнч(ЭлементИсточник) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			СкопироватьЭлементы(ЭлементПриемник, ЭлементИсточник);
		ИначеЕсли ТипЗнч(ЭлементИсточник) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
			СкопироватьЭлементы(ЭлементПриемник, ЭлементИсточник);
		ИначеЕсли ТипЗнч(ЭлементИсточник) = Тип("ПользовательскоеПолеВыборКомпоновкиДанных") Тогда
			СкопироватьЭлементы(ЭлементПриемник.Варианты, ЭлементИсточник.Варианты);
		ИначеЕсли ТипЗнч(ЭлементИсточник) = Тип("ПользовательскоеПолеВыражениеКомпоновкиДанных") Тогда
			Попытка 
				ЭлементПриемник.УстановитьВыражениеДетальныхЗаписей (ЭлементИсточник.ПолучитьВыражениеДетальныхЗаписей());
				ЭлементПриемник.УстановитьВыражениеИтоговыхЗаписей(ЭлементИсточник.ПолучитьВыражениеИтоговыхЗаписей());
				ЭлементПриемник.УстановитьПредставлениеВыраженияДетальныхЗаписей(ЭлементИсточник.ПолучитьПредставлениеВыраженияДетальныхЗаписей ());
				ЭлементПриемник.УстановитьПредставлениеВыраженияИтоговыхЗаписей(ЭлементИсточник.ПолучитьПредставлениеВыраженияИтоговыхЗаписей ());
			Исключение
			КонецПопытки
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

// Заполняет одну коллекцию элементов на основании другой
&НаСервереБезКонтекста
Процедура ЗаполнитьЭлементы(ПриемникЗначения, ИсточникЗначения, ПервыйУровень = Неопределено) Экспорт
	
	Если ТипЗнч(ПриемникЗначения) = Тип("КоллекцияЗначенийПараметровКомпоновкиДанных") Тогда
		КоллекцияЗначений = ИсточникЗначения;
	Иначе
		КоллекцияЗначений = ИсточникЗначения.Элементы;
	КонецЕсли;
	
	Для каждого ЭлементИсточник Из КоллекцияЗначений Цикл
		Если ПервыйУровень = Неопределено Тогда
			ЭлементПриемник = ПриемникЗначения.НайтиЗначениеПараметра(ЭлементИсточник.Параметр);
		Иначе
			ЭлементПриемник = ПервыйУровень.НайтиЗначениеПараметра(ЭлементИсточник.Параметр);
		КонецЕсли;
		Если ЭлементПриемник = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ЭлементПриемник, ЭлементИсточник);
		Если ТипЗнч(ЭлементИсточник) = Тип("ЗначениеПараметраКомпоновкиДанных") Тогда
			Если ЭлементИсточник.ЗначенияВложенныхПараметров.Количество() <> 0 Тогда
				ЗаполнитьЭлементы(ЭлементПриемник.ЗначенияВложенныхПараметров, ЭлементИсточник.ЗначенияВложенныхПараметров, ПриемникЗначения);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// - 04.07.13

// + 30.09.13

/////////////////////////////////////////////////////////
//	ПРОЦЕДУРЫ ВЫВОДА ДАННЫХ В ТАБЛИЧНЫХ ДОКУМЕНТ

//Вывод выгружаемых данных в табличный документ
&НаСервере
Процедура ВывестиРезультат(ТабличныйДокумент, ТекущиеДанные) 
	
	//Инициализация переменных
	ПостроительОтчетов = Новый ПостроительОтчета;
	ОбъектСервера = РеквизитФормыВЗначение("Объект");
	ОбъектСервера.ИнициализироватьМенеджерыИСообщения();
	
	//Получение типа объекта метаданных
	ЭтоРегистр = ПустаяСтрока(ТекущиеДанные.ИмяОбъектаДляЗапроса);
	Если ЭтоРегистр Тогда
		ОбъектВыборки = ТекущиеДанные.ИмяОбъектаДляЗапросаРегистра;
	Иначе
		ОбъектВыборки = ТекущиеДанные.ИмяОбъектаДляЗапроса;
	КонецЕсли;
	
	ПозТочка = Найти(ОбъектВыборки, ".");
	Если ПозТочка=0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоРегистр Тогда
		ОбъектВыборки = Лев(ОбъектВыборки, ПозТочка - 1) + "Запись." + Сред(ОбъектВыборки, ПозТочка+1);
	Иначе
		ОбъектВыборки = Лев(ОбъектВыборки, ПозТочка - 1) + "Ссылка." + Сред(ОбъектВыборки, ПозТочка+1);
	КонецЕсли;	
	
	ТекущиеДанные.Вставить("ОбъектВыборкиСтрокой", ОбъектВыборки);
	
	// показать выбранные записи	
	Если ТекущиеДанные.ИспользоватьОтбор Тогда
		НастроитьПостроитель(ОбъектСервера, ПостроительОтчетов, ТекущиеДанные);
	Иначе												
		Свойства = ОбъектСервера.Менеджеры[Тип(ТекущиеДанные.ОбъектВыборкиСтрокой)];
		
		Если Свойства = Неопределено Тогда 
			Возврат;
		КонецЕсли;
		
		ИмяТипа		= Свойства.ИмяТипа;
		
		РезультатЗапроса = ОбъектСервера.ПолучитьРезультатЗапросаДляВыгрузкиОчисткиДанных(Свойства, ИмяТипа, , , Истина);
		
		ПостроительОтчетов.ИсточникДанных = Новый ОписаниеИсточникаДанных(РезультатЗапроса);		
	КонецЕсли;
	
	ПостроительОтчетов.Параметры.Вставить("ДатаНачала", ОбъектСервера.ДатаНачала);
	ПостроительОтчетов.Параметры.Вставить("ДатаОкончания", ОбъектСервера.ДатаОкончания);
	
	ПостроительОтчетов.ЗаполнениеРасшифровки = ВидЗаполненияРасшифровкиПостроителяОтчета.Расшифровка; 

	ПостроительОтчетов.Вывести(ТабличныйДокумент);
	
	//Удалим первые 3 строки таб. документа
	ТабличныйДокумент.УдалитьОбласть(ТабличныйДокумент.Область(1,,3), ТипСмещенияТабличногоДокумента.ПоГоризонтали);
	
КонецПроцедуры

//Очистка отборов построителя 
&НаСервереБезКонтекста
Процедура УдалитьОтборыПостроителяОтчета(Построитель)
	
	ОтборКоличество = Построитель.Отбор.Количество();
	Для Н = 1 По ОтборКоличество Цикл
		Построитель.Отбор.Удалить(0);
	КонецЦикла;	
	
КонецПроцедуры

//Настройка отборов и текста построителя
&НаСервере
Процедура НастроитьПостроитель(ОбработкаОбъект, Построитель, ТекущееПВД) 
	
	ДоступностьПостроителя = ЛОЖЬ;
	
	Свойства	= ОбработкаОбъект.Менеджеры[Тип(ТекущееПВД.ОбъектВыборкиСтрокой)];
	
	Если Свойства = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если ТекущееПВД.ИмяОбъектаДляЗапросаРегистра <> "" Тогда
	
		Попытка
			
			Непериодический = НЕ Свойства.Периодический;
			ПодчиненныйРегистратору = Свойства.ПодчиненныйРегистратору;
			
			СтрокаДополненияПолейВыборкиПодчиненРегистратору = ?(НЕ ПодчиненныйРегистратору, ", NULL КАК Активность,
			|	NULL КАК Регистратор,
			|	NULL КАК НомерСтроки", "");
			
			СтрокаДополненияПолейВыборкиПериодичность = ?(Непериодический, ", NULL КАК Период", "");
			
			ИтоговоеОграничениеПоДате = "";
			
			Если ОбработкаОбъект.ИспользоватьОтборПоДатеДляВсехОбъектов Тогда
				ИтоговоеОграничениеПоДате = ОбработкаОбъект.ПолучитьСтрокуОграниченияПоДатеДляЗапроса(Свойства, Свойства.ИмяТипа, ТекущееПВД.ИмяОбъектаДляЗапросаРегистра, Ложь);
			КонецЕсли;
			
			Построитель.Текст = "ВЫБРАТЬ Разрешенные
					 |	*
					 |
					 | " + СтрокаДополненияПолейВыборкиПодчиненРегистратору + "
					 | " + СтрокаДополненияПолейВыборкиПериодичность + "
					 |
					 | ИЗ " + ТекущееПВД.ИмяОбъектаДляЗапросаРегистра + "
					 |
				 	 |" + ИтоговоеОграничениеПоДате;
					 
					 
			Построитель.Параметры.Вставить("ДатаНачала", ОбработкаОбъект.ДатаНачала);
			Построитель.Параметры.Вставить("ДатаОкончания", ОбработкаОбъект.ДатаОкончания);
										 
			Построитель.ЗаполнитьНастройки();
			ДоступностьПостроителя = Истина;		
		Исключение
			
			
		КонецПопытки;
		
	ИначеЕсли ТекущееПВД.ИмяОбъектаДляЗапроса <> "" Тогда
				
		ИтоговоеОграничениеПоДате = "";
		
		Если ОбработкаОбъект.ИспользоватьОтборПоДатеДляВсехОбъектов Тогда
			ИтоговоеОграничениеПоДате = ОбработкаОбъект.ПолучитьСтрокуОграниченияПоДатеДляЗапроса(Свойства, Свойства.ИмяТипа, "_", Ложь);
		КонецЕсли;
		
		Построитель.Текст = "ВЫБРАТЬ Разрешенные _.* ИЗ " + ТекущееПВД.ИмяОбъектаДляЗапроса + " КАК _ 
		|
		|" + ИтоговоеОграничениеПоДате + "
		|
		|{ГДЕ _.Ссылка.* КАК " + СтрЗаменить(ТекущееПВД.ИмяОбъектаДляЗапроса, ".", "_") + "}";
		
		Построитель.Параметры.Вставить("ДатаНачала", ОбработкаОбъект.ДатаНачала);
		Построитель.Параметры.Вставить("ДатаОкончания", ОбработкаОбъект.ДатаОкончания);
		
		ДоступностьПостроителя = Истина;
		
	КонецЕсли;
	
	Если ДоступностьПостроителя Тогда
		
		Построитель.Отбор.Сбросить();
		Если ТекущееПВД.НастройкаКомпоновки <> Неопределено Тогда
			
			Попытка
				ОбработкаОбъект.ЗаполнитьОтборыПостроителяПоОтборамКомпоновки(Построитель.Отбор, ТекущееПВД.НастройкаКомпоновки.Элементы, ТекущееПВД.ИмяОбъектаДляЗапроса);			
			Исключение
            	Сообщить(ОписаниеОшибки());
			КонецПопытки;
			
		КонецЕсли;
		
	Иначе
		
		УдалитьОтборыПостроителяОтчета(Построитель);
		
	КонецЕсли;
	
КонецПроцедуры


// - 30.09.13

/////////////////////////////////////////////////////////
//	КЛИЕНТСКИЕ ПРОЦЕДУРЫ ДЛЯ РАБОТЫ С ОТБОРОМ

// + 04.07.13
&НаКлиенте
Процедура ОбновитьНастройкиПостроителяВТаблицеДляВыгрузки()
	
	ТекущееПВД = Элементы.ТаблицаПравилВыгрузки.ТекущиеДанные;
	
	Если ТекущееПВД = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если Объект.КомпоновщикНастроек.Настройки.Отбор.Элементы.Количество() > 0 Тогда
		ТекущееПВД.НастройкаКомпоновки = Объект.КомпоновщикНастроек.Настройки.Отбор;
		ТекущееПВД.ИспользоватьОтбор    = ИСТИНА;
		ТекущееПВД.Включить = Истина;
	Иначе
		ТекущееПВД.НастройкаКомпоновки = Неопределено;
		ТекущееПВД.ИспользоватьОтбор    = ЛОЖЬ;
	КонецЕсли;	                                                                             
	
КонецПроцедуры

// - 04.07.13

// + 30.09.13
&НаКлиенте
Процедура ОчиститьОтборРекурсивно(Строки) 
		
	Для каждого ТекСтрока Из Строки Цикл
		ОчиститьОтборРекурсивно(ТекСтрока.ПолучитьЭлементы());	
		
		ТекСтрока.НастройкаКомпоновки = Неопределено;
		ТекСтрока.ИспользоватьОтбор = Ложь;
	КонецЦикла;
	
КонецПроцедуры

// -30.09.13

/////////////////////////////////////////////////////////
//	ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

// + 04.07.13

&НаКлиенте
Процедура ТаблицаПравилВыгрузкиПриАктивизацииСтроки(Элемент)
	
	ТекущееПВД = Элементы.ТаблицаПравилВыгрузки.ТекущиеДанные;
	
	Если ТекущееПВД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДоступностьПостроителя = ЛОЖЬ;
	
	Если НЕ (ТекущееПВД = Неопределено
		ИЛИ ТекущееПВД.ЭтоГруппа = ИСТИНА 
		ИЛИ ТекущееПВД.СпособОтбораДанных <> "СтандартнаяВыборка"
		ИЛИ (ТекущееПВД.СсылкаНаУзелОбмена <> Неопределено
		И НЕ ТекущееПВД.СсылкаНаУзелОбмена.Пустая())) Тогда
		
		ПВД = Новый Структура;
		ПВД.Вставить("НастройкиПостроителя", ТекущееПВД.НастройкаКомпоновки);
		ПВД.Вставить("ИмяОбъектаДляЗапроса", ?(ПустаяСтрока(ТекущееПВД.ИмяОбъектаДляЗапроса), 
												ТекущееПВД.ИмяОбъектаДляЗапросаРегистра,
												ТекущееПВД.ИмяОбъектаДляЗапроса));
		ДоступностьПостроителя = НастроитьОтбор(ПВД, Объект.КомпоновщикНастроек, УникальныйИдентификатор, Объект.АдресаМакетовСКД);
	КонецЕсли;
	
	Если НЕ ДоступностьПостроителя Тогда
		Элементы.ПанельОтбора.ТекущаяСтраница = Элементы.ПустаяСтраницаГруппы;
	Иначе
		Элементы.ПанельОтбора.ТекущаяСтраница = Элементы.СтраницаОтбора;
	КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастроекНастройкиОтборПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ОбновитьНастройкиПостроителяВТаблицеДляВыгрузки();
	
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастроекНастройкиОтборПослеУдаления(Элемент)
	
	ОбновитьНастройкиПостроителяВТаблицеДляВыгрузки();
	
КонецПроцедуры

&НаКлиенте
Процедура РекомендоватьОбработку(Команда)
	
	ЗапуститьПриложение("http://infostart.ru/public/193382/?rate=1");
	
КонецПроцедуры

// - 04.07.13

// + 30.09.13

&НаКлиенте
Процедура ОчиститьОтборы(Команда)
	
	ОчиститьОтборРекурсивно(Объект.ТаблицаПравилВыгрузки.ПолучитьЭлементы());
	Объект.КомпоновщикНастроек.Настройки.Отбор.Элементы.Очистить();
	
	ОбновитьНастройкиПостроителяВТаблицеДляВыгрузки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьРезультатОтбора(Команда)
	
	ТекДанные = Элементы.ТаблицаПравилВыгрузки.ТекущиеДанные;
	
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	ПараметрыОтбора = Новый Структура("ИмяОбъектаДляЗапроса, ИмяОбъектаДляЗапросаРегистра, ИспользоватьОтбор, НастройкаКомпоновки");
	ЗаполнитьЗначенияСвойств(ПараметрыОтбора, ТекДанные);
	
	ТабДок = Новый ТабличныйДокумент;
	
	ВывестиРезультат(ТабДок, ПараметрыОтбора);
	
	ТабДок.Показать();
	
КонецПроцедуры


// - 30.09.13

// - Гафуров А.А.
