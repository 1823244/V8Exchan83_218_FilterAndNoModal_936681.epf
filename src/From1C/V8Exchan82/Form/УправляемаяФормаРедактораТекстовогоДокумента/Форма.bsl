
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Проверка прав доступа должна располагаться самой первой
	Если Не ПравоДоступа("Администрирование", Метаданные) Тогда
		ВызватьИсключение НСтр("ru = 'Использование обработки в интерактивном режиме доступно только администратору.'");
	КонецЕсли;
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПолеТекстовогоДокумента(ПолеОбработчикиСобытий, "ПолеОбработчикиСобытий", Параметры.ИмяВременногоФайлаОбработчиковСобытий);
	ЗаполнитьПолеТекстовогоДокумента(ПолеОшибкиВыгрузки, "ПолеОшибкиВыгрузки", Параметры.ИмяВременногоФайлаПротоколаОбмена);
	
	Если Не Элементы.ПанельОбработчиков.ПодчиненныеЭлементы.Страница_ПолеОшибкиВыгрузки.Видимость Тогда
		Элементы.ПанельОбработчиков.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ЗаполнитьПолеТекстовогоДокумента(Объект, ИмяПоля, ИмяФайла)
	
	Если Не ПустаяСтрока(ИмяФайла) Тогда
		
		Файл = Новый Файл(ИмяФайла);
		
		Если Файл.Существует() Тогда
			
			//Заполняем данными текстовое поле
			Объект.Прочитать(ИмяФайла);
			
			//Определяем видимость страницы панели обработчиков
			ТекущаяСтраницаПанелиОбработчиков = Элементы.ПанельОбработчиков.ПодчиненныеЭлементы["Страница_" + ИмяПоля];
			
			ТекущаяСтраницаПанелиОбработчиков.Видимость = (Файл.Размер() <> 0); 
			
			//Определяем текущую страницу
			Если ТекущаяСтраницаПанелиОбработчиков.Видимость Тогда
				
				Элементы.ПанельОбработчиков.ТекущаяСтраница = ТекущаяСтраницаПанелиОбработчиков;
			
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
